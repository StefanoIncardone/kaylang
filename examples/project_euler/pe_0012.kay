##
PROBLEM: Highly Divisible Triangular Number (https://projecteuler.net/problem=12)
The sequence of triangle numbers is generated by adding the natural numbers. So the 7th triangle
number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first ten terms would be:
1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
Let us list the factors of the first seven triangle numbers:
 1: 1
 3: 1, 3
 6: 1, 2, 3, 6
10: 1, 2, 5, 10
15: 1, 3, 5, 15
21: 1, 3, 7, 21
28: 1, 2, 4, 7, 14, 28
We can see that 28 is the first triangle number to have over five divisors.

QUESTION:
What is the value of the first triangle number to have over five hundred divisors?
##

let TARGET_FACTOR_COUNT = 500;

var triangle_number = 0;
var number = 1;
var total_factor_count = 1;
loop total_factor_count <= TARGET_FACTOR_COUNT {
    total_factor_count = 1;

    triangle_number += number;
    var triangle_number_copy = triangle_number;

    var prime_factor_count = 0;
    var prime_factor = 2;
    do loop prime_factor <= triangle_number_copy {
        if triangle_number_copy % prime_factor == 0 {
            triangle_number_copy /= prime_factor;
            prime_factor_count += 1;
            continue;
        }

        total_factor_count *= prime_factor_count + 1;
        prime_factor_count = 0;

        var is_not_prime = true;
        loop is_not_prime {
            is_not_prime = false;
            prime_factor += 1;

            if prime_factor == 2 || prime_factor == 3 {
                break;
            }
            else if prime_factor <= 1 || prime_factor % 2 == 0 || prime_factor % 3 == 0 {
                continue;
            }
            else {
                var divisor = 5;
                loop divisor ** 2 <= prime_factor {
                    if prime_factor % divisor == 0 || prime_factor % (divisor + 2) == 0 {
                        is_not_prime = true;
                        break;
                    }

                    divisor += 6;
                }
            }
        }
    }

    number += 1;
}

print "expected: "; println 76576500;
print "actual:   "; println triangle_number;
