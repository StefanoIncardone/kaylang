##
PROBLEM: Largest Palindrome Product (https://projecteuler.net/problem=4)
A palindromic number reads the same both ways. The largest palindrome made from the product of two
2-digit numbers is 9009 = 91 * 99.

QUESTION:
Find the largest palindrome made from the product of two 3-digit numbers.
##

let MAX_DIGITS = 3;
let MIN_NUMBER = 10 ** (MAX_DIGITS - 1);
let MAX_NUMBER = 10 ** MAX_DIGITS - 1;

var max_palindrome = 0;

var lhs = MAX_NUMBER;
loop lhs > MIN_NUMBER {
    if max_palindrome > lhs * MAX_NUMBER { break; }

    var rhs = MAX_NUMBER;
    loop rhs > MIN_NUMBER {
        var target = lhs * rhs;
        var reversed_target = 0;

        var target_copy = target;
        loop target_copy > 0 {
            reversed_target = 10 * reversed_target + target_copy % 10;
            target_copy /= 10;
        }

        if target == reversed_target && target > max_palindrome { max_palindrome = target; }

        rhs -= 1;
    }

    lhs -= 1;
}

print "expected: "; println 906609;
print "actual:   "; println max_palindrome;
